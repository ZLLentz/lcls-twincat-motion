<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.12">
  <POU Name="FB_MotionStage" Id="{137c52b5-8983-4728-b719-31601503f20a}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_MotionStage
VAR_IN_OUT
	stMotionStage: DUT_MotionStage;
END_VAR
VAR
	fbDriveVirtual: FB_DriveVirtual;
	bExecute: BOOL;
	bFwdHit: BOOL;
	bBwdHit: BOOL;
	fbExecFallingEdge: F_TRIG;
	fbSetEnables: FB_SetEnables;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Start with an accurate status
stMotionStage.Axis.ReadStatus();

// Handle auto-enable timing
CASE stMotionStage.nEnableMode OF
	ENUM_StageEnableMode.STAGE_ENABLE_ALWAYS:
		stMotionStage.bEnable := TRUE;
		bExecute := stMotionStage.bExecute;
	ENUM_StageEnableMode.STAGE_ENABLE_NEVER:
		bExecute := stMotionStage.bExecute;
	ENUM_StageEnableMode.STAGE_ENABLE_MOTION:
		IF stMotionStage.bExecute THEN
			stMotionStage.bEnable := TRUE;
		END_IF
		bExecute := stMotionStage.bExecute AND stMotionStage.bEnableDone;
END_CASE

// Automatically fill the correct nCmdData for homing
IF stMotionStage.nCommand = ENUM_EpicsMotorCmd.EPICS_MOTOR_HOME THEN
	stMotionStage.nCmdData := stMotionStage.nHomingMode;
END_IF

fbSetEnables(stMotionStage:=stMotionStage);

// Handle standard commands using ESS's FB
fbDriveVirtual(En:=TRUE,
	bEnable:=stMotionStage.bAllEnable,
	bReset:=stMotionStage.bReset,
	bExecute:=bExecute,
	nCommand:=INT_TO_UINT(stMotionStage.nCommand),
	nCmdData:=INT_TO_UINT(stMotionStage.nCmdData),
	fVelocity:=stMotionStage.fVelocity,
	fPosition:=stMotionStage.fPosition,
	fAcceleration:=stMotionStage.fAcceleration,
	fDeceleration:=stMotionStage.fDeceleration,
	bLimitFwd:=stMotionStage.bAllForwardEnable,
	bLimitBwd:=stMotionStage.bAllBackwardEnable,
	bHomeSensor:=stMotionStage.bHome,
	fHomePosition:=stMotionStage.fHomePosition,
	bPowerSelf:=stMotionStage.bPowerSelf,
	nMotionAxisID=>stMotionStage.nMotionAxisID,
	bError=>stMotionStage.bError,
	sErrorMessage=>stMotionStage.sErrorMessage,
	Axis:=stMotionStage.Axis);

IF fbDriveVirtual.bBusy THEN
	stMotionStage.bBusy := TRUE;
	stMotionStage.bDone := FALSE;
END_IF

IF stMotionStage.bError THEN
	stMotionStage.bBusy := FALSE;
	stMotionStage.bDone := FALSE;
	stMotionStage.bEnable := FALSE;
END_IF

// Check the limits and cancel execution if appropriate. Without this block we have infinite error spam
bFwdHit := stMotionStage.Axis.Status.PositiveDirection AND NOT stMotionStage.bAllForwardEnable;
bBwdHit := stMotionStage.Axis.Status.NegativeDirection AND NOT stMotionStage.bAllBackwardEnable;
IF bFwdHit OR bBwdHit THEN
	stMotionStage.bExecute := FALSE;
END_IF

// Mark done if settled or if unexpected stop, then reset the FB if there is no error
fbExecFallingEdge(CLK:=stMotionStage.bExecute);
IF (stMotionStage.Axis.Status.InTargetPosition AND NOT fbDriveVirtual.bBusy) OR fbExecFallingEdge.Q THEN
	IF NOT stMotionStage.bDone THEN
		stMotionStage.bDone := TRUE;
		stMotionStage.bBusy := FALSE;
		IF NOT stMotionStage.Axis.Status.Error THEN
			bExecute := FALSE;
			stMotionStage.bExecute := FALSE;
		END_IF
	END_IF
END_IF
IF stMotionStage.nEnableMode = ENUM_StageEnableMode.STAGE_ENABLE_MOTION AND NOT bExecute THEN
	IF stMotionStage.Axis.Status.MotionState = MC_AXISSTATE_STANDSTILL THEN
		stMotionStage.bEnable := FALSE;
	END_IF
END_IF

// Get a definitive bEnabled reading
CASE stMotionStage.Axis.Status.MotionState OF
	// We are not enabled if there is an issue
	MC_AXISSTATE_UNDEFINED, MC_AXISSTATE_DISABLED, MC_AXISSTATE_ERRORSTOP:
		stMotionStage.bEnableDone := FALSE;
	ELSE
		stMotionStage.bEnableDone := TRUE;
END_CASE

// Handle the brake. TRUE means brake disabled.
CASE stMotionStage.nBrakeMode OF
	ENUM_StageBrakeMode.STAGE_BRAKE_DISABLED:
		stMotionStage.bBrakeRelease := stMotionStage.bEnableDone AND stMotionStage.bExecute;
	ENUM_StageBrakeMode.STAGE_BRAKE_STANDSTILL:
		IF stMotionStage.Axis.Status.MotionState = MC_AXISSTATE_STANDSTILL THEN
			stMotionStage.bBrakeRelease := FALSE;
		ELSE
			stMotionStage.bBrakeRelease := TRUE;
		END_IF
END_CASE
CASE stMotionStage.Axis.Status.MotionState OF
	MC_AXISSTATE_UNDEFINED, MC_AXISSTATE_ERRORSTOP:
		stMotionStage.bBrakeRelease := FALSE;
END_CASE

// Sync the epics status struct
stMotionStage.stAxisStatus := fbDriveVirtual.stAxisStatus;
stMotionStage.stAxisStatus.bEnabled := stMotionStage.bEnableDone;

// Reset everything when bReset is flagged
IF stMotionStage.bReset THEN
	stMotionStage.bEnable := FALSE;
	stMotionStage.bReset := FALSE;
	stMotionStage.bExecute := FALSE;
	bExecute := FALSE;
END_IF

// Ignore homing if we have the setting for it
// e.g. for pre-calibrated absolute encoders
IF stMotionStage.nHomingMode = ENUM_EpicsHomeCmd.EPICS_MOTOR_HOME_NONE THEN
	stMotionStage.stAxisStatus.bHomed := TRUE;
END_IF

// Small stuff for the Visu
stMotionStage.bVisuLowLim := NOT stMotionStage.bAllBackwardEnable;
stMotionStage.bVisuHighLim := NOT stMotionStage.bAllForwardEnable;]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>